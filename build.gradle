plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.1"
}

sourceCompatibility = JavaLanguageVersion.of(16 as int)
targetCompatibility = JavaLanguageVersion.of(16 as int)

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

processResources {
    filesMatching("plugin.yml") {
        expand "version": project.version,
                "bukkit_api_version": project.bukkit_api_version
    }

    filesMatching("compatibility.properties") {
        expand "mod_compatibility_version": project.mod_compatibility_version
    }
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation "org.bukkit:craftbukkit:${bukkit_version}"
    implementation "com.comphenix.protocol:ProtocolLib:${protocollib_version}"
    implementation "net.kyori:adventure-api:4.9.0"
    shadow "net.kyori:adventure-api:4.9.0"
    implementation "net.kyori:adventure-text-serializer-gson:4.9.0"
    shadow "net.kyori:adventure-text-serializer-gson:4.9.0"
    implementation 'de.maxhenkel.configbuilder:configbuilder:1.0.0'
    shadow 'de.maxhenkel.configbuilder:configbuilder:1.0.0'

    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

task copyJarToServer {
    doLast {
        delete {
            delete "${project.projectDir}/run/plugins/${archivesBaseName}-${version}.jar"
        }
        copy {
            from "${project.buildDir}/libs/${archivesBaseName}-${version}.jar"
            into "${project.projectDir}/run/plugins"
        }
    }
}

tasks.build.finalizedBy(tasks.copyJarToServer)

task runServer {
    doLast {
        javaexec {
            debug = true
            workingDir = "${project.projectDir}/run"
            main = "-jar";
            args = [
                    "${project.projectDir}/run/bukkit.jar",
                    "nogui"
            ]
        }
    }
}

tasks.build.dependsOn tasks.shadowJar

runServer.dependsOn(build)

shadowJar {
    configurations = [project.configurations.shadow]
    classifier ''
    relocate 'net.kyori', "de.maxhenkel.voicechat.net.kyori"
    relocate 'com.google.gson', "de.maxhenkel.voicechat.gson"
    relocate 'de.maxhenkel.configbuilder', "de.maxhenkel.voicechat.configbuilder"

    relocate 'co.aikar.commands', 'shadded.acf'
    relocate 'co.aikar.locales', 'shadded.locales'
}
